buildscript {
    ext.kotlin_version = "1.5.0"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.9.1"
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    afterEvaluate {
        if (getPlugins().hasPlugin('android') ||
            getPlugins().hasPlugin('android-library')
        ) {
            configure(android.lintOptions) {
                abortOnError false
                checkAllWarnings true
            }
        }
    }
}

task detektCheck(type: io.gitlab.arturbosch.detekt.Detekt) {
    parallel = true
    source = files(projectDir)
    include "**/*.kt"
    include "**/*.kts"
    exclude "**/resources/**"
    exclude "**/build/**"
    config.setFrom(files("$rootDir/detekt.yml"))
    buildUponDefaultConfig = false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
